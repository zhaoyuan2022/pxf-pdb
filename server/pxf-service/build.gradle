apply plugin: "org.springframework.boot"

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    /*******************************
     * API Dependencies
     *******************************/

    /*******************************
     * Compile Only Dependencies
     *******************************/

    compileOnly("org.apache.hadoop:hadoop-annotations:${hadoopVersion}") { transitive = false }

    /*******************************
     * Implementation Dependencies
     *******************************/

    implementation(project(':pxf-api'))
    implementation(project(':pxf-hbase'))
    implementation(project(':pxf-hdfs'))
    implementation(project(':pxf-hive'))
    implementation(project(':pxf-jdbc'))
    implementation(project(':pxf-json'))
    implementation(project(':pxf-s3'))
    implementation(project(':pxf-diagnostic'))

    implementation("com.google.guava:guava")
    implementation("commons-codec:commons-codec")
    implementation("commons-collections:commons-collections")
    implementation("commons-lang:commons-lang")
    implementation("org.springframework.boot:spring-boot-starter-log4j2")
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('io.micrometer:micrometer-registry-prometheus')

    implementation("org.apache.hadoop:hadoop-hdfs-client:${hadoopVersion}") { transitive = false }

    /*******************************
     * These JARs below are needed for Java 11
     * jcenter doesn't have full com.sun.xml.bind:jaxb-core/jaxb-impl packages,
     * using glassfish distro
     *******************************/

    implementation("javax.xml.bind:jaxb-api:2.3.0") { transitive = false }
    implementation("org.glassfish.jaxb:jaxb-core:2.3.0") { transitive = false }
    implementation("org.glassfish.jaxb:jaxb-runtime:2.3.0") { transitive = false }
    implementation("com.sun.istack:istack-commons-runtime:3.0.8") { transitive = false }
    implementation("javax.activation:activation:1.1.1") { transitive = false }

    /*******************************
     * Test Dependencies
     *******************************/

    testCompileOnly("org.apache.hadoop:hadoop-annotations:${hadoopVersion}")
    testImplementation("com.esotericsoftware:kryo:3.0.3")
    testImplementation("commons-io:commons-io")
    testImplementation("org.simplify4u:slf4j-mock:2.1.0") // for MDC mocking

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.boot:spring-boot-starter-webflux')
}

bootJar {
    manifest {
        attributes("Implementation-Title": project.name)
        attributes("Implementation-Version": project.version)
        attributes('Main-Class': 'org.springframework.boot.loader.PropertiesLauncher')
    }
}

springBoot {
    mainClass = "org.greenplum.pxf.service.PxfServiceApplication"
    buildInfo {
        properties {
            additional = [
                    "pxfApiVersion" : "${apiVersion}"
            ]
        }
    }
}

test {
    useJUnitPlatform()
}
